buildscript {
    ext.getExtOrDefault = { name -> return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['TrackPlayer_' + name]
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:8.7.2"
        classpath "com.facebook.react:react-native-gradle-plugin"
        // noinspection DifferentKotlinGradleVersion
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "com.facebook.react"

def getExtOrIntegerDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["TrackPlayer_" + name]).toInteger()
}

android {
    compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

    defaultConfig {
        minSdkVersion getExtOrIntegerDefault("minSdkVersion")
        targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        disable "GradleCompatible"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    mavenCentral()
    google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
    implementation "com.facebook.react:react-android"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // ExoPlayer
    implementation "androidx.media3:media3-exoplayer:1.0.2"
    implementation "androidx.media3:media3-session:1.0.2"
    implementation "androidx.media3:media3-exoplayer-dash:1.0.2"
    implementation "androidx.media3:media3-exoplayer-hls:1.0.2"
    implementation "androidx.media3:media3-exoplayer-smoothstreaming:1.0.2"

    // Make sure we're using androidx
    implementation "androidx.core:core:1.9.0"
    implementation "androidx.media:media:1.6.0"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0"
    implementation "com.github.bumptech.glide:glide:4.12.0"
}

react {
    jsRootDir = file("../src/")
    libraryName = "TrackPlayer"
    codegenJavaPackageName = "com.guichaguri.trackplayer"
}
