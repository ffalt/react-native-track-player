import groovy.json.JsonSlurper

buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
    }
}

apply plugin: 'com.android.library'

boolean dash = false, hls = false, smoothstreaming = false

File file = file('../../../track-player.json')
if(file.exists()) {
    def json = new JsonSlurper().parseText(file.text)
    dash = json.dash ?: dash
    hls = json.hls ?: hls
    smoothstreaming = json.smoothstreaming ?: smoothstreaming
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet("compileSdkVersion", 28)

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 16) // RN's minimum version
        targetSdkVersion safeExtGet("targetSdkVersion", 28)

        versionCode 1
        versionName "1.0"

        consumerProguardFiles 'proguard-rules.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven {
        // As RN is not updated in jcenter anymore, we'll have to grab it from npm
        // Make sure you have installed the react-native npm package before compiling
        url "../node_modules/react-native/android"
    }

    mavenCentral()
    google()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'

    // ExoPlayer Core
    implementation "androidx.media3:media3-exoplayer:1.0.2"
    implementation "androidx.media3:media3-session:1.0.2"

    // ExoPlayer DASH
    if (dash) {
        implementation "androidx.media3:media3-exoplayer-dash:1.0.2"
    } else {
        compileOnly "androidx.media3:media3-exoplayer-dash:1.0.2"
    }

    // ExoPlayer HLS
    if (hls) {
        implementation "androidx.media3:media3-exoplayer-hls:1.0.2"
    } else {
        compileOnly "androidx.media3:media3-exoplayer-hls:1.0.2"
    }

    // ExoPlayer SmoothStreaming
    if (smoothstreaming) {
        implementation "androidx.media3:media3-exoplayer-smoothstreaming:1.0.2"
    } else {
        compileOnly "androidx.media3:media3-exoplayer-smoothstreaming:1.0.2"
    }

    // Make sure we're using androidx
    implementation "androidx.core:core:1.9.0"
    implementation "androidx.media:media:1.6.0"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0"
    implementation "com.github.bumptech.glide:glide:4.12.0"
}
