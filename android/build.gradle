import groovy.json.JsonSlurper

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
   }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}

apply plugin: 'com.android.library'

boolean dash = false, hls = false, smoothstreaming = false

File file = file('../../../track-player.json')
if(file.exists()) {
    def json = new JsonSlurper().parseText(file.text)
    dash = json.dash ?: dash
    hls = json.hls ?: hls
    smoothstreaming = json.smoothstreaming ?: smoothstreaming
}

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet("compileSdkVersion", 28)

    defaultConfig {
        minSdkVersion safeExtGet("minSdkVersion", 16) // RN's minimum version
        targetSdkVersion safeExtGet("targetSdkVersion", 28)

        versionCode 1
        versionName "1.0"

        consumerProguardFiles 'proguard-rules.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven { url 'https://www.jitpack.io' }
        jcenter()
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation 'com.facebook.react:react-native:+'

    def exoPlayerVersion = safeExtGet("exoPlayerVersion", '2.11.4')

    // ExoPlayer Core
    implementation "com.google.android.exoplayer:exoplayer-core:${exoPlayerVersion}"

    // ExoPlayer DASH
    if (dash) {
        implementation "com.google.android.exoplayer:exoplayer-dash:${exoPlayerVersion}"
    } else {
        compileOnly "com.google.android.exoplayer:exoplayer-dash:${exoPlayerVersion}"
    }

    // ExoPlayer HLS
    if (hls) {
        implementation "com.google.android.exoplayer:exoplayer-hls:${exoPlayerVersion}"
    } else {
        compileOnly "com.google.android.exoplayer:exoplayer-hls:${exoPlayerVersion}"
    }

    // ExoPlayer SmoothStreaming
    if (smoothstreaming) {
        implementation "com.google.android.exoplayer:exoplayer-smoothstreaming:${exoPlayerVersion}"
    } else {
        compileOnly "com.google.android.exoplayer:exoplayer-smoothstreaming:${exoPlayerVersion}"
    }

    // Make sure we're using androidx
    implementation "androidx.core:core:1.3.0-rc01"
    implementation "androidx.media:media:1.1.0"
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.1.0-alpha01"
    implementation "com.github.bumptech.glide:glide:4.11.0"
}
